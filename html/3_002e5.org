[[#pagetop][⇡]]<<pagetop>><<g_t3_002e5>>
Next: [[file:Chapter-4.xhtml#Chapter-4][Chapter 4]], Prev: [[file:3_002e4.xhtml#g_t3_002e4][3.4]], Up: [[file:Chapter-3.xhtml#Chapter-3][Chapter 3]]   [[[file:index.xhtml#SEC_Contents][Contents]]]

<<Streams>>
*** 3.5Streams
    :PROPERTIES:
    :CUSTOM_ID: streams
    :CLASS: section
    :END:

We've gained a good understanding of assignment as a tool in modeling, as well as an appreciation of the complex problems that assignment raises. It is time to ask whether we could have gone about things in a different way, so as to avoid some of these problems. In this section, we explore an alternative approach to modeling state, based on data structures called <<index-streams-1>> /streams/. As we shall see, streams can mitigate some of the complexity of modeling state.

Let's step back and review where this complexity comes from. In an attempt to model real-world phenomena, we made some apparently reasonable decisions: We modeled real-world objects with local state by computational objects with local variables. We identified time variation in the real world with time variation in the computer. We implemented the time variation of the states of the model objects in the computer with assignments to the local variables of the model objects.

Is there another approach? Can we avoid identifying time in the computer with time in the modeled world? Must we make the model change with time in order to model phenomena in a changing world? Think about the issue in terms of mathematical functions. We can describe the time-varying behavior of a quantity [[file:fig/math/2f4b15565d0a1018e90c3e1b30b76acc.svg]] as a function of time [[file:fig/math/11951e6ec01ad0675327321cf040c65a.svg]]. If we concentrate on [[file:fig/math/2f4b15565d0a1018e90c3e1b30b76acc.svg]] instant by instant, we think of it as a changing quantity. Yet if we concentrate on the entire time history of values, we do not emphasize change---the function itself does not change.^{[[#FOOT180][180]]}

If time is measured in discrete steps, then we can model a time function as a (possibly infinite) sequence. In this section, we will see how to model change in terms of sequences that represent the time histories of the systems being modeled. To accomplish this, we introduce new data structures called <<index-streams-2>> /streams/. From an abstract point of view, a stream is simply a sequence. However, we will find that the straightforward implementation of streams as lists (as in [[file:2_002e2.xhtml#g_t2_002e2_002e1][2.2.1]]) doesn't fully reveal the power of stream processing. As an alternative, we introduce the technique of <<index-delayed-evaluation-1>> /delayed evaluation/, which enables us to represent very large (even infinite) sequences as streams.

Stream processing lets us model systems that have state without ever using assignment or mutable data. This has important implications, both theoretical and practical, because we can build models that avoid the drawbacks inherent in introducing assignment. On the other hand, the stream framework raises difficulties of its own, and the question of which modeling technique leads to more modular and more easily maintained systems remains open.

<<g_t3_002e5_002e1>> <<Streams-Are-Delayed-Lists>>
**** 3.5.1Streams Are Delayed Lists
     :PROPERTIES:
     :CUSTOM_ID: streams-are-delayed-lists
     :CLASS: subsection
     :END:

As we saw in [[file:2_002e2.xhtml#g_t2_002e2_002e3][2.2.3]], sequences can serve as standard interfaces for combining program modules. We formulated powerful abstractions for manipulating sequences, such as =map=, =filter=, and =accumulate=, that capture a wide variety of operations in a manner that is both succinct and elegant.

Unfortunately, if we represent sequences as lists, this elegance is bought at the price of severe inefficiency with respect to both the time and space required by our computations. When we represent manipulations on sequences as transformations of lists, our programs must construct and copy data structures (which may be huge) at every step of a process.

To see why this is true, let us compare two programs for computing the sum of all the prime numbers in an interval. The first program is written in standard iterative style:^{[[#FOOT181][181]]}

#+BEGIN_SRC lisp
    (define (sum-primes a b)
      (define (iter count accum)
        (cond ((> count b) accum)
              ((prime? count)
               (iter (+ count 1)
                     (+ count accum)))
              (else (iter (+ count 1) accum))))
      (iter a 0))
#+END_SRC

The second program performs the same computation using the sequence operations of [[file:2_002e2.xhtml#g_t2_002e2_002e3][2.2.3]]:

#+BEGIN_SRC lisp
    (define (sum-primes a b)
      (accumulate 
       +
       0
       (filter prime? (enumerate-interval a b))))
#+END_SRC

In carrying out the computation, the first program needs to store only the sum being accumulated. In contrast, the filter in the second program cannot do any testing until =enumerate-interval= has constructed a complete list of the numbers in the interval. The filter generates another list, which in turn is passed to =accumulate= before being collapsed to form a sum. Such large intermediate storage is not needed by the first program, which we can think of as enumerating the interval incrementally, adding each prime to the sum as it is generated.

The inefficiency in using lists becomes painfully apparent if we use the sequence paradigm to compute the second prime in the interval from 10,000 to 1,000,000 by evaluating the expression

#+BEGIN_SRC lisp
    (car (cdr 
          (filter 
           prime?
           (enumerate-interval 10000 1000000))))
#+END_SRC

This expression does find the second prime, but the computational overhead is outrageous. We construct a list of almost a million integers, filter this list by testing each element for primality, and then ignore almost all of the result. In a more traditional programming style, we would interleave the enumeration and the filtering, and stop when we reached the second prime.

Streams are a clever idea that allows one to use sequence manipulations without incurring the costs of manipulating sequences as lists. With streams we can achieve the best of both worlds: We can formulate programs elegantly as sequence manipulations, while attaining the efficiency of incremental computation. The basic idea is to arrange to construct a stream only partially, and to pass the partial construction to the program that consumes the stream. If the consumer attempts to access a part of the stream that has not yet been constructed, the stream will automatically construct just enough more of itself to produce the required part, thus preserving the illusion that the entire stream exists. In other words, although we will write programs as if we were processing complete sequences, we design our stream implementation to automatically and transparently interleave the construction of the stream with its use.

On the surface, streams are just lists with different names for the procedures that manipulate them. There is a constructor, =cons-stream=, and two selectors, =stream-car= and =stream-cdr=, which satisfy the constraints

#+BEGIN_EXAMPLE
    (stream-car (cons-stream x y)) = x
    (stream-cdr (cons-stream x y)) = y
#+END_EXAMPLE

There is a distinguishable object, =the-empty-stream=, which cannot be the result of any =cons-stream= operation, and which can be identified with the predicate =stream-null?=.^{[[#FOOT182][182]]} Thus we can make and use streams, in just the same way as we can make and use lists, to represent aggregate data arranged in a sequence. In particular, we can build stream analogs of the list operations from [[file:Chapter-2.xhtml#Chapter-2][Chapter 2]], such as =list-ref=, =map=, and =for-each=:^{[[#FOOT183][183]]}

#+BEGIN_SRC lisp
    (define (stream-ref s n)
      (if (= n 0)
          (stream-car s)
          (stream-ref (stream-cdr s) (- n 1))))

    (define (stream-map proc s)
      (if (stream-null? s)
          the-empty-stream
          (cons-stream 
           (proc (stream-car s))
           (stream-map proc (stream-cdr s)))))

    (define (stream-for-each proc s)
      (if (stream-null? s)
          'done
          (begin 
            (proc (stream-car s))
            (stream-for-each proc 
                             (stream-cdr s)))))
#+END_SRC

=Stream-for-each= is useful for viewing streams:

#+BEGIN_SRC lisp
    (define (display-stream s)
      (stream-for-each display-line s))

    (define (display-line x)
      (newline)
      (display x))
#+END_SRC

To make the stream implementation automatically and transparently interleave the construction of a stream with its use, we will arrange for the =cdr= of a stream to be evaluated when it is accessed by the =stream-cdr= procedure rather than when the stream is constructed by =cons-stream=. This implementation choice is reminiscent of our discussion of rational numbers in [[file:2_002e1.xhtml#g_t2_002e1_002e2][2.1.2]], where we saw that we can choose to implement rational numbers so that the reduction of numerator and denominator to lowest terms is performed either at construction time or at selection time. The two rational-number implementations produce the same data abstraction, but the choice has an effect on efficiency. There is a similar relationship between streams and ordinary lists. As a data abstraction, streams are the same as lists. The difference is the time at which the elements are evaluated. With ordinary lists, both the =car= and the =cdr= are evaluated at construction time. With streams, the =cdr= is evaluated at selection time.

Our implementation of streams will be based on a special form called =delay=. Evaluating =(delay ⟨exp⟩)= does not evaluate the expression =⟨=exp=⟩=, but rather returns a so-called <<index-delayed-object>> /delayed object/, which we can think of as a “promise” to evaluate =⟨=exp=⟩= at some future time. As a companion to =delay=, there is a procedure called =force= that takes a delayed object as argument and performs the evaluation---in effect, forcing the =delay= to fulfill its promise. We will see below how =delay= and =force= can be implemented, but first let us use these to construct streams.

=Cons-stream= is a special form defined so that

#+BEGIN_SRC lisp
    (cons-stream ⟨a⟩ ⟨b⟩)
#+END_SRC

is equivalent to

#+BEGIN_SRC lisp
    (cons ⟨a⟩ (delay ⟨b⟩))
#+END_SRC

What this means is that we will construct streams using pairs. However, rather than placing the value of the rest of the stream into the =cdr= of the pair we will put there a promise to compute the rest if it is ever requested. =Stream-car= and =stream-cdr= can now be defined as procedures:

#+BEGIN_SRC lisp
    (define (stream-car stream) 
      (car stream))

    (define (stream-cdr stream) 
      (force (cdr stream)))
#+END_SRC

=Stream-car= selects the =car= of the pair; =stream-cdr= selects the =cdr= of the pair and evaluates the delayed expression found there to obtain the rest of the stream.^{[[#FOOT184][184]]}

<<The-stream-implementation-in-action>>
***** The stream implementation in action
      :PROPERTIES:
      :CUSTOM_ID: the-stream-implementation-in-action
      :CLASS: subsubheading
      :END:

To see how this implementation behaves, let us analyze the “outrageous” prime computation we saw above, reformulated in terms of streams:

#+BEGIN_SRC lisp
    (stream-car 
     (stream-cdr
      (stream-filter 
       prime? (stream-enumerate-interval 
               10000 1000000))))
#+END_SRC

We will see that it does indeed work efficiently.

We begin by calling =stream-enumerate-interval= with the arguments 10,000 and 1,000,000. =Stream-enumerate-interval= is the stream analog of =enumerate-interval= ([[file:2_002e2.xhtml#g_t2_002e2_002e3][2.2.3]]):

#+BEGIN_SRC lisp
    (define (stream-enumerate-interval low high)
      (if (> low high)
          the-empty-stream
          (cons-stream
           low
           (stream-enumerate-interval (+ low 1)
                                      high))))
#+END_SRC

and thus the result returned by =stream-enumerate-interval=, formed by the =cons-stream=, is^{[[#FOOT185][185]]}

#+BEGIN_SRC lisp
    (cons 10000
          (delay 
            (stream-enumerate-interval 
             10001 
             1000000)))
#+END_SRC

That is, =stream-enumerate-interval= returns a stream represented as a pair whose =car= is 10,000 and whose =cdr= is a promise to enumerate more of the interval if so requested. This stream is now filtered for primes, using the stream analog of the =filter= procedure ([[file:2_002e2.xhtml#g_t2_002e2_002e3][2.2.3]]):

#+BEGIN_SRC lisp
    (define (stream-filter pred stream)
      (cond ((stream-null? stream) 
             the-empty-stream)
            ((pred (stream-car stream))
             (cons-stream 
              (stream-car stream)
              (stream-filter 
               pred
               (stream-cdr stream))))
            (else (stream-filter 
                   pred 
                   (stream-cdr stream)))))
#+END_SRC

=Stream-filter= tests the =stream-car= of the stream (the =car= of the pair, which is 10,000). Since this is not prime, =stream-filter= examines the =stream-cdr= of its input stream. The call to =stream-cdr= forces evaluation of the delayed =stream-enumerate-interval=, which now returns

#+BEGIN_SRC lisp
    (cons 10001
          (delay 
            (stream-enumerate-interval 
             10002 
             1000000)))
#+END_SRC

=Stream-filter= now looks at the =stream-car= of this stream, 10,001, sees that this is not prime either, forces another =stream-cdr=, and so on, until =stream-enumerate-interval= yields the prime 10,007, whereupon =stream-filter=, according to its definition, returns

#+BEGIN_SRC lisp
    (cons-stream 
     (stream-car stream)
     (stream-filter pred (stream-cdr stream)))
#+END_SRC

which in this case is

#+BEGIN_SRC lisp
    (cons 10007
          (delay
            (stream-filter
             prime?
             (cons 10008
                   (delay
                     (stream-enumerate-interval 
                      10009 1000000))))))
#+END_SRC

This result is now passed to =stream-cdr= in our original expression. This forces the delayed =stream-filter=, which in turn keeps forcing the delayed =stream-enumerate-interval= until it finds the next prime, which is 10,009. Finally, the result passed to =stream-car= in our original expression is

#+BEGIN_SRC lisp
    (cons 10009
          (delay
            (stream-filter
             prime?
             (cons 10010
                   (delay
                     (stream-enumerate-interval 
                      10011 1000000))))))
#+END_SRC

=Stream-car= returns 10,009, and the computation is complete. Only as many integers were tested for primality as were necessary to find the second prime, and the interval was enumerated only as far as was necessary to feed the prime filter.

In general, we can think of delayed evaluation as “demand-driven” programming, whereby each stage in the stream process is activated only enough to satisfy the next stage. What we have done is to decouple the actual order of events in the computation from the apparent structure of our procedures. We write procedures as if the streams existed “all at once” when, in reality, the computation is performed incrementally, as in traditional programming styles.

<<Implementing-delay-and-force>>
***** Implementing =delay= and =force=
      :PROPERTIES:
      :CUSTOM_ID: implementing-delay-and-force
      :CLASS: subsubheading
      :END:

Although =delay= and =force= may seem like mysterious operations, their implementation is really quite straightforward. =Delay= must package an expression so that it can be evaluated later on demand, and we can accomplish this simply by treating the expression as the body of a procedure. =Delay= can be a special form such that

#+BEGIN_SRC lisp
    (delay ⟨exp⟩)
#+END_SRC

is syntactic sugar for

#+BEGIN_SRC lisp
    (lambda () ⟨exp⟩)
#+END_SRC

=Force= simply calls the procedure (of no arguments) produced by =delay=, so we can implement =force= as a procedure:

#+BEGIN_SRC lisp
    (define (force delayed-object)
      (delayed-object))
#+END_SRC

This implementation suffices for =delay= and =force= to work as advertised, but there is an important optimization that we can include. In many applications, we end up forcing the same delayed object many times. This can lead to serious inefficiency in recursive programs involving streams. (See [[#Exercise-3_002e57][Exercise 3.57]].) The solution is to build delayed objects so that the first time they are forced, they store the value that is computed. Subsequent forcings will simply return the stored value without repeating the computation. In other words, we implement =delay= as a special-purpose memoized procedure similar to the one described in [[file:3_002e3.xhtml#Exercise-3_002e27][Exercise 3.27]]. One way to accomplish this is to use the following procedure, which takes as argument a procedure (of no arguments) and returns a memoized version of the procedure. The first time the memoized procedure is run, it saves the computed result. On subsequent evaluations, it simply returns the result.

#+BEGIN_SRC lisp
    (define (memo-proc proc)
      (let ((already-run? false) (result false))
        (lambda ()
          (if (not already-run?)
              (begin (set! result (proc))
                     (set! already-run? true)
                     result)
              result))))
#+END_SRC

=Delay= is then defined so that =(delay ⟨exp⟩)= is equivalent to

#+BEGIN_SRC lisp
    (memo-proc (lambda () ⟨exp⟩))
#+END_SRC

and =force= is as defined previously.^{[[#FOOT186][186]]}

#+BEGIN_QUOTE
  *<<Exercise-3_002e50>>Exercise 3.50:* Complete the following definition, which generalizes =stream-map= to allow procedures that take multiple arguments, analogous to =map= in [[file:2_002e2.xhtml#g_t2_002e2_002e1][2.2.1]], [[file:2_002e2.xhtml#Footnote-78][Footnote 78]].

  #+BEGIN_SRC lisp
      (define (stream-map proc . argstreams)
        (if (⟨??⟩ (car argstreams))
            the-empty-stream
            (⟨??⟩
             (apply proc (map ⟨??⟩ argstreams))
             (apply stream-map
                    (cons proc 
                          (map ⟨??⟩ 
                               argstreams))))))
  #+END_SRC

#+END_QUOTE

#+BEGIN_QUOTE
  *<<Exercise-3_002e51>>Exercise 3.51:* In order to take a closer look at delayed evaluation, we will use the following procedure, which simply returns its argument after printing it:

  #+BEGIN_SRC lisp
      (define (show x)
        (display-line x)
        x)
  #+END_SRC

  What does the interpreter print in response to evaluating each expression in the following sequence?^{[[#FOOT187][187]]}

  #+BEGIN_SRC lisp
      (define x 
        (stream-map 
         show 
         (stream-enumerate-interval 0 10)))

      (stream-ref x 5)
      (stream-ref x 7)
  #+END_SRC

#+END_QUOTE

#+BEGIN_QUOTE
  *<<Exercise-3_002e52>>Exercise 3.52:* Consider the sequence of expressions

  #+BEGIN_SRC lisp
      (define sum 0)

      (define (accum x)
        (set! sum (+ x sum))
        sum)

      (define seq 
        (stream-map 
         accum 
         (stream-enumerate-interval 1 20)))

      (define y (stream-filter even? seq))

      (define z 
        (stream-filter 
         (lambda (x) 
           (= (remainder x 5) 0)) seq))

      (stream-ref y 7)
      (display-stream z)
  #+END_SRC

  What is the value of =sum= after each of the above expressions is evaluated? What is the printed response to evaluating the =stream-ref= and =display-stream= expressions? Would these responses differ if we had implemented =(delay ⟨exp⟩)= simply as =(lambda () ⟨exp⟩)= without using the optimization provided by =memo-proc=? Explain.
#+END_QUOTE

<<g_t3_002e5_002e2>> <<Infinite-Streams>>
**** 3.5.2Infinite Streams
     :PROPERTIES:
     :CUSTOM_ID: infinite-streams
     :CLASS: subsection
     :END:

We have seen how to support the illusion of manipulating streams as complete entities even though, in actuality, we compute only as much of the stream as we need to access. We can exploit this technique to represent sequences efficiently as streams, even if the sequences are very long. What is more striking, we can use streams to represent sequences that are infinitely long. For instance, consider the following definition of the stream of positive integers:

#+BEGIN_SRC lisp
    (define (integers-starting-from n)
      (cons-stream 
       n (integers-starting-from (+ n 1))))
    (define integers (integers-starting-from 1))
#+END_SRC

This makes sense because =integers= will be a pair whose =car= is 1 and whose =cdr= is a promise to produce the integers beginning with 2. This is an infinitely long stream, but in any given time we can examine only a finite portion of it. Thus, our programs will never know that the entire infinite stream is not there.

Using =integers= we can define other infinite streams, such as the stream of integers that are not divisible by 7:

#+BEGIN_SRC lisp
    (define (divisible? x y) (= (remainder x y) 0))
    (define no-sevens
      (stream-filter (lambda (x) 
                       (not (divisible? x 7)))
                     integers))
#+END_SRC

Then we can find integers not divisible by 7 simply by accessing elements of this stream:

#+BEGIN_SRC lisp
    (stream-ref no-sevens 100)
    117
#+END_SRC

In analogy with =integers=, we can define the infinite stream of Fibonacci numbers:

#+BEGIN_SRC lisp
    (define (fibgen a b)
      (cons-stream a (fibgen b (+ a b))))
    (define fibs (fibgen 0 1))
#+END_SRC

=Fibs= is a pair whose =car= is 0 and whose =cdr= is a promise to evaluate =(fibgen 1 1)=. When we evaluate this delayed =(fibgen 1 1)=, it will produce a pair whose =car= is 1 and whose =cdr= is a promise to evaluate =(fibgen 1 2)=, and so on.

For a look at a more exciting infinite stream, we can generalize the =no-sevens= example to construct the infinite stream of prime numbers, using a method known as the <<index-sieve-of-Eratosthenes>> /sieve of Eratosthenes/.^{[[#FOOT188][188]]} We start with the integers beginning with 2, which is the first prime. To get the rest of the primes, we start by filtering the multiples of 2 from the rest of the integers. This leaves a stream beginning with 3, which is the next prime. Now we filter the multiples of 3 from the rest of this stream. This leaves a stream beginning with 5, which is the next prime, and so on. In other words, we construct the primes by a sieving process, described as follows: To sieve a stream =S=, form a stream whose first element is the first element of =S= and the rest of which is obtained by filtering all multiples of the first element of =S= out of the rest of =S= and sieving the result. This process is readily described in terms of stream operations:

#+BEGIN_SRC lisp
    (define (sieve stream)
      (cons-stream
       (stream-car stream)
       (sieve (stream-filter
               (lambda (x)
                 (not (divisible? 
                       x (stream-car stream))))
               (stream-cdr stream)))))

    (define primes 
      (sieve (integers-starting-from 2)))
#+END_SRC

Now to find a particular prime we need only ask for it:

#+BEGIN_SRC lisp
    (stream-ref primes 50)
    233
#+END_SRC

It is interesting to contemplate the signal-processing system set up by =sieve=, shown in the “Henderson diagram” in [[#Figure-3_002e31][Figure 3.31]].^{[[#FOOT189][189]]} The input stream feeds into an “un=cons=er” that separates the first element of the stream from the rest of the stream. The first element is used to construct a divisibility filter, through which the rest is passed, and the output of the filter is fed to another sieve box. Then the original first element is =cons=ed onto the output of the internal sieve to form the output stream. Thus, not only is the stream infinite, but the signal processor is also infinite, because the sieve contains a sieve within it.

#+CAPTION: *Figure 3.31:* The prime sieve viewed as a signal-processing system.
[[file:fig/chap3/Fig3.31a.std.svg]]

<<Defining-streams-implicitly>>
***** Defining streams implicitly
      :PROPERTIES:
      :CUSTOM_ID: defining-streams-implicitly
      :CLASS: subsubheading
      :END:

The =integers= and =fibs= streams above were defined by specifying “generating” procedures that explicitly compute the stream elements one by one. An alternative way to specify streams is to take advantage of delayed evaluation to define streams implicitly. For example, the following expression defines the stream =ones= to be an infinite stream of ones:

#+BEGIN_SRC lisp
    (define ones (cons-stream 1 ones))
#+END_SRC

This works much like the definition of a recursive procedure: =ones= is a pair whose =car= is 1 and whose =cdr= is a promise to evaluate =ones=. Evaluating the =cdr= gives us again a 1 and a promise to evaluate =ones=, and so on.

We can do more interesting things by manipulating streams with operations such as =add-streams=, which produces the elementwise sum of two given streams:^{[[#FOOT190][190]]}

#+BEGIN_SRC lisp
    (define (add-streams s1 s2) 
      (stream-map + s1 s2))
#+END_SRC

Now we can define the integers as follows:

#+BEGIN_SRC lisp
    (define integers 
      (cons-stream 1 (add-streams ones integers)))
#+END_SRC

This defines =integers= to be a stream whose first element is 1 and the rest of which is the sum of =ones= and =integers=. Thus, the second element of =integers= is 1 plus the first element of =integers=, or 2; the third element of =integers= is 1 plus the second element of =integers=, or 3; and so on. This definition works because, at any point, enough of the =integers= stream has been generated so that we can feed it back into the definition to produce the next integer.

We can define the Fibonacci numbers in the same style:

#+BEGIN_SRC lisp
    (define fibs 
      (cons-stream 
       0 (cons-stream
          1 (add-streams 
             (stream-cdr fibs) fibs))))
#+END_SRC

This definition says that =fibs= is a stream beginning with 0 and 1, such that the rest of the stream can be generated by adding =fibs= to itself shifted by one place:

#+BEGIN_EXAMPLE
        1 1 2 3 5  8 13 21 … = (stream-cdr fibs)
        0 1 1 2 3  5  8 13 … = fibs
    0 1 1 2 3 5 8 13 21 34 … = fibs
#+END_EXAMPLE

=Scale-stream= is another useful procedure in formulating such stream definitions. This multiplies each item in a stream by a given constant:

#+BEGIN_SRC lisp
    (define (scale-stream stream factor)
      (stream-map
       (lambda (x) (* x factor))
       stream))
#+END_SRC

For example,

#+BEGIN_SRC lisp
    (define double 
      (cons-stream 1 (scale-stream double 2)))
#+END_SRC

produces the stream of powers of 2: 1, 2, 4, 8, 16, 32, ....

An alternate definition of the stream of primes can be given by starting with the integers and filtering them by testing for primality. We will need the first prime, 2, to get started:

#+BEGIN_SRC lisp
    (define primes
      (cons-stream
       2 (stream-filter 
          prime? (integers-starting-from 3))))
#+END_SRC

This definition is not so straightforward as it appears, because we will test whether a number [[file:fig/math/0932467390da34555ec70c122d7e915e.svg]] is prime by checking whether [[file:fig/math/0932467390da34555ec70c122d7e915e.svg]] is divisible by a prime (not by just any integer) less than or equal to [[file:fig/math/6401acda6e40516e261b2a2cd409285f.svg]]:

#+BEGIN_SRC lisp
    (define (prime? n)
      (define (iter ps)
        (cond ((> (square (stream-car ps)) n) true)
              ((divisible? n (stream-car ps)) false)
              (else (iter (stream-cdr ps)))))
      (iter primes))
#+END_SRC

This is a recursive definition, since =primes= is defined in terms of the =prime?= predicate, which itself uses the =primes= stream. The reason this procedure works is that, at any point, enough of the =primes= stream has been generated to test the primality of the numbers we need to check next. That is, for every [[file:fig/math/0932467390da34555ec70c122d7e915e.svg]] we test for primality, either [[file:fig/math/0932467390da34555ec70c122d7e915e.svg]] is not prime (in which case there is a prime already generated that divides it) or [[file:fig/math/0932467390da34555ec70c122d7e915e.svg]] is prime (in which case there is a prime already generated---i.e., a prime less than [[file:fig/math/0932467390da34555ec70c122d7e915e.svg]]---that is greater than [[file:fig/math/6401acda6e40516e261b2a2cd409285f.svg]]).^{[[#FOOT191][191]]}

#+BEGIN_QUOTE
  *<<Exercise-3_002e53>>Exercise 3.53:* Without running the program, describe the elements of the stream defined by

  #+BEGIN_SRC lisp
      (define s (cons-stream 1 (add-streams s s)))
  #+END_SRC

#+END_QUOTE

#+BEGIN_QUOTE
  *<<Exercise-3_002e54>>Exercise 3.54:* Define a procedure =mul-streams=, analogous to =add-streams=, that produces the elementwise product of its two input streams. Use this together with the stream of =integers= to complete the following definition of the stream whose [[file:fig/math/3b6c232f796b2d291446b22cb38c933e.svg]] element (counting from 0) is [[file:fig/math/c09177a287583da634d642d340852664.svg]] factorial:

  #+BEGIN_SRC lisp
      (define factorials 
        (cons-stream 1 (mul-streams ⟨??⟩ ⟨??⟩)))
  #+END_SRC

#+END_QUOTE

#+BEGIN_QUOTE
  *<<Exercise-3_002e55>>Exercise 3.55:* Define a procedure =partial-sums= that takes as argument a stream [[file:fig/math/fbd79508b7054f548dfce169e251714c.svg]] and returns the stream whose elements are [[file:fig/math/ccdfed3cc477c16c89ce8a73b91c28ef.svg]], [[file:fig/math/ee7ffbb904b0f776955765746bcaeb0b.svg]], [[file:fig/math/a8737afc40de7b9afc7fe8d0857ac6cd.svg]]. For example, =(partial-sums integers)= should be the stream 1, 3, 6, 10, 15, ....
#+END_QUOTE

#+BEGIN_QUOTE
  *<<Exercise-3_002e56>>Exercise 3.56:* A famous problem, first raised by R. Hamming, is to enumerate, in ascending order with no repetitions, all positive integers with no prime factors other than 2, 3, or 5. One obvious way to do this is to simply test each integer in turn to see whether it has any factors other than 2, 3, and 5. But this is very inefficient, since, as the integers get larger, fewer and fewer of them fit the requirement. As an alternative, let us call the required stream of numbers =S= and notice the following facts about it.

  - =S= begins with 1.
  - The elements of =(scale-stream S 2)= are also elements of =S=.
  - The same is true for =(scale-stream S 3)= and =(scale-stream S 5)=.
  - These are all the elements of =S=.

  Now all we have to do is combine elements from these sources. For this we define a procedure =merge= that combines two ordered streams into one ordered result stream, eliminating repetitions:

  #+BEGIN_SRC lisp
      (define (merge s1 s2)
        (cond ((stream-null? s1) s2)
              ((stream-null? s2) s1)
              (else
               (let ((s1car (stream-car s1))
                     (s2car (stream-car s2)))
                 (cond ((< s1car s2car)
                        (cons-stream 
                         s1car 
                         (merge (stream-cdr s1) 
                                s2)))
                       ((> s1car s2car)
                        (cons-stream 
                         s2car 
                         (merge s1 
                                (stream-cdr s2))))
                       (else
                        (cons-stream 
                         s1car
                         (merge 
                          (stream-cdr s1)
                          (stream-cdr s2)))))))))
  #+END_SRC

  Then the required stream may be constructed with =merge=, as follows:

  #+BEGIN_SRC lisp
      (define S (cons-stream 1 (merge ⟨??⟩ ⟨??⟩)))
  #+END_SRC

  Fill in the missing expressions in the places marked =⟨??⟩= above.
#+END_QUOTE

#+BEGIN_QUOTE
  *<<Exercise-3_002e57>>Exercise 3.57:* How many additions are performed when we compute the [[file:fig/math/3b6c232f796b2d291446b22cb38c933e.svg]] Fibonacci number using the definition of =fibs= based on the =add-streams= procedure? Show that the number of additions would be exponentially greater if we had implemented =(delay ⟨exp⟩)= simply as =(lambda () ⟨exp⟩)=, without using the optimization provided by the =memo-proc= procedure described in [[#g_t3_002e5_002e1][3.5.1]].^{[[#FOOT192][192]]}
#+END_QUOTE

#+BEGIN_QUOTE
  *<<Exercise-3_002e58>>Exercise 3.58:* Give an interpretation of the stream computed by the following procedure:

  #+BEGIN_SRC lisp
      (define (expand num den radix)
        (cons-stream
         (quotient (* num radix) den)
         (expand (remainder (* num radix) den) 
                 den 
                 radix)))
  #+END_SRC

  (=Quotient= is a primitive that returns the integer quotient of two integers.) What are the successive elements produced by =(expand 1 7 10)=? What is produced by =(expand 3 8 10)=?
#+END_QUOTE

#+BEGIN_QUOTE
  *<<Exercise-3_002e59>>Exercise 3.59:* In [[file:2_002e5.xhtml#g_t2_002e5_002e3][2.5.3]] we saw how to implement a polynomial arithmetic system representing polynomials as lists of terms. In a similar way, we can work with <<index-power-series>> /power series/, such as [[file:fig/math/0104eb9f4856fb63489a041a0b2ddc66.svg]] represented as infinite streams. We will represent the series [[file:fig/math/4496bd4d695f4824e8b6388ed4203b19.svg]] as the stream whose elements are the coefficients [[file:fig/math/a165fae8b3379b5b86caf620688ab9d6.svg]], [[file:fig/math/0419040fb91e96b48ec2d15ad4081c9a.svg]], [[file:fig/math/c5d8ae8f215f7fe9c23b45926b8bdeb5.svg]], [[file:fig/math/8a81aae27c0eb62c227a6a850310ec41.svg]], ....

  1. The integral of the series [[file:fig/math/b60fa91d48bd906a10c9cb7ef4b6ca23.svg]] is the series [[file:fig/math/ee5b5c55345aaca8f2b5d0bdf2a25eca.svg]] where [[file:fig/math/b98937403abaaeb001eed7d68959205d.svg]] is any constant. Define a procedure =integrate-series= that takes as input a stream [[file:fig/math/a165fae8b3379b5b86caf620688ab9d6.svg]], [[file:fig/math/0419040fb91e96b48ec2d15ad4081c9a.svg]], [[file:fig/math/c5d8ae8f215f7fe9c23b45926b8bdeb5.svg]], ... representing a power series and returns the stream [[file:fig/math/a165fae8b3379b5b86caf620688ab9d6.svg]], [[file:fig/math/6bbba4b78e9b464563753dfeca03ad56.svg]], [[file:fig/math/e8f660c8fb2fc1a84834adcdbe3e761a.svg]], ... of coefficients of the non-constant terms of the integral of the series. (Since the result has no constant term, it doesn't represent a power series; when we use =integrate-series=, we will =cons= on the appropriate constant.)
  2. The function [[file:fig/math/ba99f9d43d44373e1faf54b4887893ba.svg]] is its own derivative. This implies that [[file:fig/math/7c7f7091f6e090480d15653cbccc9f87.svg]] and the integral of [[file:fig/math/7c7f7091f6e090480d15653cbccc9f87.svg]] are the same series, except for the constant term, which is [[file:fig/math/ff764142a6191db1e12e236158133d92.svg]]. Accordingly, we can generate the series for [[file:fig/math/7c7f7091f6e090480d15653cbccc9f87.svg]] as

     #+BEGIN_SRC lisp
         (define exp-series
           (cons-stream 
            1 (integrate-series exp-series)))
     #+END_SRC

     Show how to generate the series for sine and cosine, starting from the facts that the derivative of sine is cosine and the derivative of cosine is the negative of sine:

     #+BEGIN_SRC lisp
         (define cosine-series 
           (cons-stream 1 ⟨??⟩))

         (define sine-series
           (cons-stream 0 ⟨??⟩))
     #+END_SRC

#+END_QUOTE

#+BEGIN_QUOTE
  *<<Exercise-3_002e60>>Exercise 3.60:* With power series represented as streams of coefficients as in [[#Exercise-3_002e59][Exercise 3.59]], adding series is implemented by =add-streams=. Complete the definition of the following procedure for multiplying series:

  #+BEGIN_SRC lisp
      (define (mul-series s1 s2)
        (cons-stream ⟨??⟩ (add-streams ⟨??⟩ ⟨??⟩)))
  #+END_SRC

  You can test your procedure by verifying that [[file:fig/math/9137e4d48f233be5aa98f19cb4bc2240.svg]] using the series from [[#Exercise-3_002e59][Exercise 3.59]].
#+END_QUOTE

#+BEGIN_QUOTE
  *<<Exercise-3_002e61>>Exercise 3.61:* Let [[file:fig/math/fbd79508b7054f548dfce169e251714c.svg]] be a power series ([[#Exercise-3_002e59][Exercise 3.59]]) whose constant term is 1. Suppose we want to find the power series [[file:fig/math/6c3df8009c7580f22bf7baa9a6548072.svg]], that is, the series [[file:fig/math/208302b3852d881c7fd29d7f7f689076.svg]] such that [[file:fig/math/6e6a917c96c36ebf22d814046002328f.svg]]. Write [[file:fig/math/9976cb94fccbfbb49d7052165a95847d.svg]] where [[file:fig/math/ff11b3ba1c957a11d39563918ad3e434.svg]] is the part of [[file:fig/math/fbd79508b7054f548dfce169e251714c.svg]] after the constant term. Then we can solve for [[file:fig/math/208302b3852d881c7fd29d7f7f689076.svg]] as follows: [[file:fig/math/7a0782831bf0b65c402837d602c27076.svg]] In other words, [[file:fig/math/208302b3852d881c7fd29d7f7f689076.svg]] is the power series whose constant term is 1 and whose higher-order terms are given by the negative of [[file:fig/math/ff11b3ba1c957a11d39563918ad3e434.svg]] times [[file:fig/math/208302b3852d881c7fd29d7f7f689076.svg]]. Use this idea to write a procedure =invert-unit-series= that computes [[file:fig/math/6c3df8009c7580f22bf7baa9a6548072.svg]] for a power series [[file:fig/math/fbd79508b7054f548dfce169e251714c.svg]] with constant term 1. You will need to use =mul-series= from [[#Exercise-3_002e60][Exercise 3.60]].
#+END_QUOTE

#+BEGIN_QUOTE
  *<<Exercise-3_002e62>>Exercise 3.62:* Use the results of [[#Exercise-3_002e60][Exercise 3.60]] and [[#Exercise-3_002e61][Exercise 3.61]] to define a procedure =div-series= that divides two power series. =Div-series= should work for any two series, provided that the denominator series begins with a nonzero constant term. (If the denominator has a zero constant term, then =div-series= should signal an error.) Show how to use =div-series= together with the result of [[#Exercise-3_002e59][Exercise 3.59]] to generate the power series for tangent.
#+END_QUOTE

<<g_t3_002e5_002e3>> <<Exploiting-the-Stream-Paradigm>>
**** 3.5.3Exploiting the Stream Paradigm
     :PROPERTIES:
     :CUSTOM_ID: exploiting-the-stream-paradigm
     :CLASS: subsection
     :END:

Streams with delayed evaluation can be a powerful modeling tool, providing many of the benefits of local state and assignment. Moreover, they avoid some of the theoretical tangles that accompany the introduction of assignment into a programming language.

The stream approach can be illuminating because it allows us to build systems with different module boundaries than systems organized around assignment to state variables. For example, we can think of an entire time series (or signal) as a focus of interest, rather than the values of the state variables at individual moments. This makes it convenient to combine and compare components of state from different moments.

<<Formulating-iterations-as-stream-processes>>
***** Formulating iterations as stream processes
      :PROPERTIES:
      :CUSTOM_ID: formulating-iterations-as-stream-processes
      :CLASS: subsubheading
      :END:

In section [[file:1_002e2.xhtml#g_t1_002e2_002e1][1.2.1]], we introduced iterative processes, which proceed by updating state variables. We know now that we can represent state as a “timeless” stream of values rather than as a set of variables to be updated. Let's adopt this perspective in revisiting the square-root procedure from [[file:1_002e1.xhtml#g_t1_002e1_002e7][1.1.7]]. Recall that the idea is to generate a sequence of better and better guesses for the square root of [[file:fig/math/2f4b15565d0a1018e90c3e1b30b76acc.svg]] by applying over and over again the procedure that improves guesses:

#+BEGIN_SRC lisp
    (define (sqrt-improve guess x)
      (average guess (/ x guess)))
#+END_SRC

In our original =sqrt= procedure, we made these guesses be the successive values of a state variable. Instead we can generate the infinite stream of guesses, starting with an initial guess of 1:^{[[#FOOT193][193]]}

#+BEGIN_SRC lisp
    (define (sqrt-stream x)
      (define guesses
        (cons-stream 
         1.0 (stream-map
              (lambda (guess)
                (sqrt-improve guess x))
              guesses)))
      guesses)

    (display-stream (sqrt-stream 2))
    1.
    1.5
    1.4166666666666665
    1.4142156862745097
    1.4142135623746899
    …
#+END_SRC

We can generate more and more terms of the stream to get better and better guesses. If we like, we can write a procedure that keeps generating terms until the answer is good enough. (See [[#Exercise-3_002e64][Exercise 3.64]].)

Another iteration that we can treat in the same way is to generate an approximation to [[file:fig/math/c25469cd205e957b38b51203870f48af.svg]], based upon the alternating series that we saw in [[file:1_002e3.xhtml#g_t1_002e3_002e1][1.3.1]]: [[file:fig/math/dccad749df98b6f22231cf93aa7d5db8.svg]] We first generate the stream of summands of the series (the reciprocals of the odd integers, with alternating signs). Then we take the stream of sums of more and more terms (using the =partial-sums= procedure of [[#Exercise-3_002e55][Exercise 3.55]]) and scale the result by 4:

#+BEGIN_SRC lisp
    (define (pi-summands n)
      (cons-stream 
       (/ 1.0 n)
       (stream-map - (pi-summands (+ n 2)))))

    (define pi-stream
      (scale-stream 
       (partial-sums (pi-summands 1)) 4))

    (display-stream pi-stream)
    4.
    2.666666666666667
    3.466666666666667
    2.8952380952380956
    3.3396825396825403
    2.9760461760461765
    3.2837384837384844
    3.017071817071818
    …
#+END_SRC

This gives us a stream of better and better approximations to [[file:fig/math/c25469cd205e957b38b51203870f48af.svg]], although the approximations converge rather slowly. Eight terms of the sequence bound the value of [[file:fig/math/c25469cd205e957b38b51203870f48af.svg]] between 3.284 and 3.017.

So far, our use of the stream of states approach is not much different from updating state variables. But streams give us an opportunity to do some interesting tricks. For example, we can transform a stream with a <<index-sequence-accelerator>> /sequence accelerator/ that converts a sequence of approximations to a new sequence that converges to the same value as the original, only faster.

One such accelerator, due to the eighteenth-century Swiss mathematician Leonhard Euler, works well with sequences that are partial sums of alternating series (series of terms with alternating signs). In Euler's technique, if [[file:fig/math/0c3097352e9e517a82f406c0dab738c5.svg]] is the [[file:fig/math/3b6c232f796b2d291446b22cb38c933e.svg]] term of the original sum sequence, then the accelerated sequence has terms [[file:fig/math/2b2c6dbadd5306218e802a992e2042c1.svg]] Thus, if the original sequence is represented as a stream of values, the transformed sequence is given by

#+BEGIN_SRC lisp
    (define (euler-transform s)
      (let ((s0 (stream-ref s 0))     ; Sₙ₋₁
            (s1 (stream-ref s 1))     ; Sₙ
            (s2 (stream-ref s 2)))    ; Sₙ₊₁
        (cons-stream 
         (- s2 (/ (square (- s2 s1))
                  (+ s0 (* -2 s1) s2)))
         (euler-transform (stream-cdr s)))))
#+END_SRC

We can demonstrate Euler acceleration with our sequence of approximations to [[file:fig/math/c25469cd205e957b38b51203870f48af.svg]]:

#+BEGIN_SRC lisp
    (display-stream 
     (euler-transform pi-stream))
    3.166666666666667
    3.1333333333333337
    3.1452380952380956
    3.13968253968254
    3.1427128427128435
    3.1408813408813416
    3.142071817071818
    3.1412548236077655
    …
#+END_SRC

Even better, we can accelerate the accelerated sequence, and recursively accelerate that, and so on. Namely, we create a stream of streams (a structure we'll call a <<index-tableau>> /tableau/) in which each stream is the transform of the preceding one:

#+BEGIN_SRC lisp
    (define (make-tableau transform s)
      (cons-stream 
       s
       (make-tableau
        transform
        (transform s))))
#+END_SRC

The tableau has the form [[file:fig/math/77e9263cdf29b1cdcfafe50d8c769c77.svg]] Finally, we form a sequence by taking the first term in each row of the tableau:

#+BEGIN_SRC lisp
    (define (accelerated-sequence transform s)
      (stream-map stream-car
                  (make-tableau transform s)))
#+END_SRC

We can demonstrate this kind of “super-acceleration” of the [[file:fig/math/c25469cd205e957b38b51203870f48af.svg]] sequence:

#+BEGIN_SRC lisp
    (display-stream 
     (accelerated-sequence euler-transform
                           pi-stream))
    4.
    3.166666666666667
    3.142105263157895
    3.141599357319005
    3.1415927140337785
    3.1415926539752927
    3.1415926535911765
    3.141592653589778
    …
#+END_SRC

The result is impressive. Taking eight terms of the sequence yields the correct value of [[file:fig/math/c25469cd205e957b38b51203870f48af.svg]] to 14 decimal places. If we had used only the original [[file:fig/math/c25469cd205e957b38b51203870f48af.svg]] sequence, we would need to compute on the order of [[file:fig/math/bc8a508d060402364809c7241de1fd24.svg]] terms (i.e., expanding the series far enough so that the individual terms are less than [[file:fig/math/6623e063c97439587c9fb4cb9c9a259a.svg]]) to get that much accuracy!

We could have implemented these acceleration techniques without using streams. But the stream formulation is particularly elegant and convenient because the entire sequence of states is available to us as a data structure that can be manipulated with a uniform set of operations.

#+BEGIN_QUOTE
  *<<Exercise-3_002e63>>Exercise 3.63:* Louis Reasoner asks why the =sqrt-stream= procedure was not written in the following more straightforward way, without the local variable =guesses=:

  #+BEGIN_SRC lisp
      (define (sqrt-stream x)
        (cons-stream 
         1.0
         (stream-map (lambda (guess)
                       (sqrt-improve guess x))
                     (sqrt-stream x))))
  #+END_SRC

  Alyssa P. Hacker replies that this version of the procedure is considerably less efficient because it performs redundant computation. Explain Alyssa's answer. Would the two versions still differ in efficiency if our implementation of =delay= used only =(lambda () ⟨exp⟩)= without using the optimization provided by =memo-proc= ([[#g_t3_002e5_002e1][3.5.1]])?
#+END_QUOTE

#+BEGIN_QUOTE
  *<<Exercise-3_002e64>>Exercise 3.64:* Write a procedure =stream-limit= that takes as arguments a stream and a number (the tolerance). It should examine the stream until it finds two successive elements that differ in absolute value by less than the tolerance, and return the second of the two elements. Using this, we could compute square roots up to a given tolerance by

  #+BEGIN_SRC lisp
      (define (sqrt x tolerance)
        (stream-limit (sqrt-stream x) tolerance))
  #+END_SRC

#+END_QUOTE

#+BEGIN_QUOTE
  *<<Exercise-3_002e65>>Exercise 3.65:* Use the series [[file:fig/math/fdc0728957f44bb9d6ff65e1c4083dca.svg]] to compute three sequences of approximations to the natural logarithm of 2, in the same way we did above for [[file:fig/math/c25469cd205e957b38b51203870f48af.svg]]. How rapidly do these sequences converge?
#+END_QUOTE

<<Infinite-streams-of-pairs>>
***** Infinite streams of pairs
      :PROPERTIES:
      :CUSTOM_ID: infinite-streams-of-pairs
      :CLASS: subsubheading
      :END:

In [[file:2_002e2.xhtml#g_t2_002e2_002e3][2.2.3]], we saw how the sequence paradigm handles traditional nested loops as processes defined on sequences of pairs. If we generalize this technique to infinite streams, then we can write programs that are not easily represented as loops, because the “looping” must range over an infinite set.

For example, suppose we want to generalize the =prime-sum-pairs= procedure of [[file:2_002e2.xhtml#g_t2_002e2_002e3][2.2.3]] to produce the stream of pairs of /all/ integers [[file:fig/math/b3e51b500a677738d763b9243179a091.svg]] with [[file:fig/math/abd99fa84a4de8dd35d5caa76adaef23.svg]] such that [[file:fig/math/c6ad57c179a45a768d358dff2d5b5da3.svg]] is prime. If =int-pairs= is the sequence of all pairs of integers [[file:fig/math/b3e51b500a677738d763b9243179a091.svg]] with [[file:fig/math/abd99fa84a4de8dd35d5caa76adaef23.svg]], then our required stream is simply^{[[#FOOT194][194]]}

#+BEGIN_SRC lisp
    (stream-filter 
     (lambda (pair)
       (prime? (+ (car pair) (cadr pair))))
     int-pairs)
#+END_SRC

Our problem, then, is to produce the stream =int-pairs=. More generally, suppose we have two streams [[file:fig/math/3768dd4f99fd2a21fd89391488ce9e6b.svg]] and [[file:fig/math/e742abfaa74a2f4365231ad408c4dd1d.svg]], and imagine the infinite rectangular array [[file:fig/math/4a8232c5c1bdf2f1b9a255a444c529e9.svg]] We wish to generate a stream that contains all the pairs in the array that lie on or above the diagonal, i.e., the pairs [[file:fig/math/6cf696a75cc83fcb9d1afbebebb276ac.svg]] (If we take both [[file:fig/math/fbd79508b7054f548dfce169e251714c.svg]] and [[file:fig/math/f06926aab0bc0e9f47ee2cd90f581bb2.svg]] to be the stream of integers, then this will be our desired stream =int-pairs=.)

Call the general stream of pairs =(pairs S T)=, and consider it to be composed of three parts: the pair [[file:fig/math/bd66f5cdaad4cea3d736fd9bd57b8e4f.svg]], the rest of the pairs in the first row, and the remaining pairs:^{[[#FOOT195][195]]} [[file:fig/math/222e6625a866defe42456b9ec507580a.svg]] Observe that the third piece in this decomposition (pairs that are not in the first row) is (recursively) the pairs formed from =(stream-cdr S)= and =(stream-cdr T)=. Also note that the second piece (the rest of the first row) is

#+BEGIN_SRC lisp
    (stream-map (lambda (x) 
                  (list (stream-car s) x))
                (stream-cdr t))
#+END_SRC

Thus we can form our stream of pairs as follows:

#+BEGIN_SRC lisp
    (define (pairs s t)
      (cons-stream
       (list (stream-car s) (stream-car t))
       (⟨combine-in-some-way⟩
        (stream-map (lambda (x) 
                      (list (stream-car s) x))
                    (stream-cdr t))
        (pairs (stream-cdr s)
               (stream-cdr t)))))
#+END_SRC

In order to complete the procedure, we must choose some way to combine the two inner streams. One idea is to use the stream analog of the =append= procedure from [[file:2_002e2.xhtml#g_t2_002e2_002e1][2.2.1]]:

#+BEGIN_SRC lisp
    (define (stream-append s1 s2)
      (if (stream-null? s1)
          s2
          (cons-stream 
           (stream-car s1)
           (stream-append (stream-cdr s1) s2))))
#+END_SRC

This is unsuitable for infinite streams, however, because it takes all the elements from the first stream before incorporating the second stream. In particular, if we try to generate all pairs of positive integers using

#+BEGIN_SRC lisp
    (pairs integers integers)
#+END_SRC

our stream of results will first try to run through all pairs with the first integer equal to 1, and hence will never produce pairs with any other value of the first integer.

To handle infinite streams, we need to devise an order of combination that ensures that every element will eventually be reached if we let our program run long enough. An elegant way to accomplish this is with the following =interleave= procedure:^{[[#FOOT196][196]]}

#+BEGIN_SRC lisp
    (define (interleave s1 s2)
      (if (stream-null? s1)
          s2
          (cons-stream 
           (stream-car s1)
           (interleave s2 (stream-cdr s1)))))
#+END_SRC

Since =interleave= takes elements alternately from the two streams, every element of the second stream will eventually find its way into the interleaved stream, even if the first stream is infinite.

We can thus generate the required stream of pairs as

#+BEGIN_SRC lisp
    (define (pairs s t)
      (cons-stream
       (list (stream-car s) (stream-car t))
       (interleave
        (stream-map (lambda (x) 
                      (list (stream-car s) x))
                    (stream-cdr t))
        (pairs (stream-cdr s) (stream-cdr t)))))
#+END_SRC

#+BEGIN_QUOTE
  *<<Exercise-3_002e66>>Exercise 3.66:* Examine the stream =(pairs integers integers)=. Can you make any general comments about the order in which the pairs are placed into the stream? For example, approximately how many pairs precede the pair (1, 100)? the pair (99, 100)? the pair (100, 100)? (If you can make precise mathematical statements here, all the better. But feel free to give more qualitative answers if you find yourself getting bogged down.)
#+END_QUOTE

#+BEGIN_QUOTE
  *<<Exercise-3_002e67>>Exercise 3.67:* Modify the =pairs= procedure so that =(pairs integers integers)= will produce the stream of /all/ pairs of integers [[file:fig/math/b3e51b500a677738d763b9243179a091.svg]] (without the condition [[file:fig/math/abd99fa84a4de8dd35d5caa76adaef23.svg]]). Hint: You will need to mix in an additional stream.
#+END_QUOTE

#+BEGIN_QUOTE
  *<<Exercise-3_002e68>>Exercise 3.68:* Louis Reasoner thinks that building a stream of pairs from three parts is unnecessarily complicated. Instead of separating the pair [[file:fig/math/bd66f5cdaad4cea3d736fd9bd57b8e4f.svg]] from the rest of the pairs in the first row, he proposes to work with the whole first row, as follows:

  #+BEGIN_SRC lisp
      (define (pairs s t)
        (interleave
         (stream-map
          (lambda (x) 
            (list (stream-car s) x))
          t)
         (pairs (stream-cdr s)
                (stream-cdr t))))
  #+END_SRC

  Does this work? Consider what happens if we evaluate =(pairs integers integers)= using Louis's definition of =pairs=.
#+END_QUOTE

#+BEGIN_QUOTE
  *<<Exercise-3_002e69>>Exercise 3.69:* Write a procedure =triples= that takes three infinite streams, [[file:fig/math/fbd79508b7054f548dfce169e251714c.svg]], [[file:fig/math/f06926aab0bc0e9f47ee2cd90f581bb2.svg]], and [[file:fig/math/0458f32a47ca1e6b3ebaefdc8bf8c1cf.svg]], and produces the stream of triples [[file:fig/math/c87ecfd93abb37a528dfd168dfcd6409.svg]] such that [[file:fig/math/2db15d184b94b315ee79b6ceff30c731.svg]]. Use =triples= to generate the stream of all Pythagorean triples of positive integers, i.e., the triples [[file:fig/math/e7853946c0c2678998e1da658f89fd35.svg]] such that [[file:fig/math/abd99fa84a4de8dd35d5caa76adaef23.svg]] and [[file:fig/math/91f53e5e64b2c77fdad283a4e7195096.svg]].
#+END_QUOTE

#+BEGIN_QUOTE
  *<<Exercise-3_002e70>>Exercise 3.70:* It would be nice to be able to generate streams in which the pairs appear in some useful order, rather than in the order that results from an /ad hoc/ interleaving process. We can use a technique similar to the =merge= procedure of [[#Exercise-3_002e56][Exercise 3.56]], if we define a way to say that one pair of integers is “less than” another. One way to do this is to define a “weighting function” [[file:fig/math/8139212770180f78c0fcf1f13ea1c129.svg]] and stipulate that [[file:fig/math/4369a830cbdbbc9579d6d56d0951ce88.svg]] is less than [[file:fig/math/14c033748be0eca7d8b32c51020a45ff.svg]] if [[file:fig/math/ea7b56c0a8d85ac835bfdaa3b79030ab.svg]]. Write a procedure =merge-weighted= that is like =merge=, except that =merge-weighted= takes an additional argument =weight=, which is a procedure that computes the weight of a pair, and is used to determine the order in which elements should appear in the resulting merged stream.^{[[#FOOT197][197]]} Using this, generalize =pairs= to a procedure =weighted-pairs= that takes two streams, together with a procedure that computes a weighting function, and generates the stream of pairs, ordered according to weight. Use your procedure to generate

  1. the stream of all pairs of positive integers [[file:fig/math/b3e51b500a677738d763b9243179a091.svg]] with [[file:fig/math/abd99fa84a4de8dd35d5caa76adaef23.svg]] ordered according to the sum [[file:fig/math/c6ad57c179a45a768d358dff2d5b5da3.svg]],
  2. the stream of all pairs of positive integers [[file:fig/math/b3e51b500a677738d763b9243179a091.svg]] with [[file:fig/math/abd99fa84a4de8dd35d5caa76adaef23.svg]], where neither [[file:fig/math/aa5dfb3bb62785181553d83502ccb9ec.svg]] nor [[file:fig/math/c43c70f4cf72247cdeb1dd81fb4f1ee0.svg]] is divisible by 2, 3, or 5, and the pairs are ordered according to the sum [[file:fig/math/7cf7773f8e26e88b87af89c46bd47bc6.svg]].
#+END_QUOTE

#+BEGIN_QUOTE
  *<<Exercise-3_002e71>>Exercise 3.71:* Numbers that can be expressed as the sum of two cubes in more than one way are sometimes called <<index-Ramanujan-numbers>> /Ramanujan numbers/, in honor of the mathematician Srinivasa Ramanujan.^{[[#FOOT198][198]]} Ordered streams of pairs provide an elegant solution to the problem of computing these numbers. To find a number that can be written as the sum of two cubes in two different ways, we need only generate the stream of pairs of integers [[file:fig/math/b3e51b500a677738d763b9243179a091.svg]] weighted according to the sum [[file:fig/math/1e628ba2e98ce7a6f4ea5488126a8495.svg]] (see [[#Exercise-3_002e70][Exercise 3.70]]), then search the stream for two consecutive pairs with the same weight. Write a procedure to generate the Ramanujan numbers. The first such number is 1,729. What are the next five?
#+END_QUOTE

#+BEGIN_QUOTE
  *<<Exercise-3_002e72>>Exercise 3.72:* In a similar way to [[#Exercise-3_002e71][Exercise 3.71]] generate a stream of all numbers that can be written as the sum of two squares in three different ways (showing how they can be so written).
#+END_QUOTE

<<Streams-as-signals>>
***** Streams as signals
      :PROPERTIES:
      :CUSTOM_ID: streams-as-signals
      :CLASS: subsubheading
      :END:

We began our discussion of streams by describing them as computational analogs of the “signals” in signal-processing systems. In fact, we can use streams to model signal-processing systems in a very direct way, representing the values of a signal at successive time intervals as consecutive elements of a stream. For instance, we can implement an <<index-integrator>> /integrator/ or <<index-summer>> /summer/ that, for an input stream [[file:fig/math/f2f7ad008ff77a18900341213f807da0.svg]], an initial value [[file:fig/math/684e3ac6700a3aa191a0a61645d5b384.svg]], and a small increment [[file:fig/math/d3c1074774c34911f99ecb81f361716d.svg]], accumulates the sum [[file:fig/math/320cf002a4855ac505055af11e8f1a4e.svg]] and returns the stream of values [[file:fig/math/3768dd4f99fd2a21fd89391488ce9e6b.svg]]. The following =integral= procedure is reminiscent of the “implicit style” definition of the stream of integers ([[#g_t3_002e5_002e2][3.5.2]]):

#+BEGIN_SRC lisp
    (define (integral integrand initial-value dt)
      (define int
        (cons-stream 
         initial-value
         (add-streams (scale-stream integrand dt)
                      int)))
      int)
#+END_SRC

[[#Figure-3_002e32][Figure 3.32]] is a picture of a signal-processing system that corresponds to the =integral= procedure. The input stream is scaled by [[file:fig/math/d3c1074774c34911f99ecb81f361716d.svg]] and passed through an adder, whose output is passed back through the same adder. The self-reference in the definition of =int= is reflected in the figure by the feedback loop that connects the output of the adder to one of the inputs.

#+CAPTION: *Figure 3.32:* The =integral= procedure viewed as a signal-processing system.
[[file:fig/chap3/Fig3.32a.std.svg]]

#+BEGIN_QUOTE
  *<<Exercise-3_002e73>>Exercise 3.73:* We can model electrical circuits using streams to represent the values of currents or voltages at a sequence of times. For instance, suppose we have an <<index-RC-circuit>> /RC circuit/ consisting of a resistor of resistance [[file:fig/math/0048ca7d12c133aea4d1304bedc83542.svg]] and a capacitor of capacitance [[file:fig/math/684e3ac6700a3aa191a0a61645d5b384.svg]] in series. The voltage response [[file:fig/math/71942c801c8d60cf7d5f705ba88631d7.svg]] of the circuit to an injected current [[file:fig/math/aa5dfb3bb62785181553d83502ccb9ec.svg]] is determined by the formula in [[#Figure-3_002e33][Figure 3.33]], whose structure is shown by the accompanying signal-flow diagram.

  #+CAPTION: *Figure 3.33:* An RC circuit and the associated signal-flow diagram.
  [[file:fig/chap3/Fig3.33a.std.svg]]

  Write a procedure =RC= that models this circuit. =RC= should take as inputs the values of [[file:fig/math/0048ca7d12c133aea4d1304bedc83542.svg]], [[file:fig/math/684e3ac6700a3aa191a0a61645d5b384.svg]], and [[file:fig/math/d3c1074774c34911f99ecb81f361716d.svg]] and should return a procedure that takes as inputs a stream representing the current [[file:fig/math/aa5dfb3bb62785181553d83502ccb9ec.svg]] and an initial value for the capacitor voltage [[file:fig/math/defc98e806040bf97fe8b8fa234f5500.svg]] and produces as output the stream of voltages [[file:fig/math/71942c801c8d60cf7d5f705ba88631d7.svg]]. For example, you should be able to use =RC= to model an RC circuit with [[file:fig/math/0048ca7d12c133aea4d1304bedc83542.svg]] = 5 ohms, [[file:fig/math/684e3ac6700a3aa191a0a61645d5b384.svg]] = 1 farad, and a 0.5-second time step by evaluating =(define RC1 (RC 5 1 0.5))=. This defines =RC1= as a procedure that takes a stream representing the time sequence of currents and an initial capacitor voltage and produces the output stream of voltages.
#+END_QUOTE

#+BEGIN_QUOTE
  *<<Exercise-3_002e74>>Exercise 3.74:* Alyssa P. Hacker is designing a system to process signals coming from physical sensors. One important feature she wishes to produce is a signal that describes the <<index-zero-crossings>> /zero crossings/ of the input signal. That is, the resulting signal should be [[file:fig/math/d6848d11ac9f4fb573601777b943a529.svg]] whenever the input signal changes from negative to positive, [[file:fig/math/f414ba729a14753d41004ffe71140ba9.svg]] whenever the input signal changes from positive to negative, and [[file:fig/math/dea28aa47a858eacfd9e22fe41c999f0.svg]] otherwise. (Assume that the sign of a [[file:fig/math/dea28aa47a858eacfd9e22fe41c999f0.svg]] input is positive.) For example, a typical input signal with its associated zero-crossing signal would be

  #+BEGIN_SRC lisp
      … 1 2 1.5 1 0.5 -0.1 -2 -3 -2 -0.5 0.2 3 4 …
      … 0 0  0  0  0   -1   0  0  0   0   1  0 0 …
  #+END_SRC

  In Alyssa's system, the signal from the sensor is represented as a stream =sense-data= and the stream =zero-crossings= is the corresponding stream of zero crossings. Alyssa first writes a procedure =sign-change-detector= that takes two values as arguments and compares the signs of the values to produce an appropriate [[file:fig/math/dea28aa47a858eacfd9e22fe41c999f0.svg]], [[file:fig/math/8fddb65f811d9e1f6a1703617a4e84ba.svg]], or [[file:fig/math/f414ba729a14753d41004ffe71140ba9.svg]]. She then constructs her zero-crossing stream as follows:

  #+BEGIN_SRC lisp
      (define (make-zero-crossings
               input-stream last-value)
        (cons-stream
         (sign-change-detector 
          (stream-car input-stream) 
          last-value)
         (make-zero-crossings 
          (stream-cdr input-stream)
          (stream-car input-stream))))

      (define zero-crossings 
        (make-zero-crossings sense-data 0))
  #+END_SRC

  Alyssa's boss, Eva Lu Ator, walks by and suggests that this program is approximately equivalent to the following one, which uses the generalized version of =stream-map= from [[#Exercise-3_002e50][Exercise 3.50]]:

  #+BEGIN_SRC lisp
      (define zero-crossings
        (stream-map sign-change-detector 
                    sense-data 
                    ⟨expression⟩))
  #+END_SRC

  Complete the program by supplying the indicated =⟨=expression=⟩=.
#+END_QUOTE

#+BEGIN_QUOTE
  *<<Exercise-3_002e75>>Exercise 3.75:* Unfortunately, Alyssa's zero-crossing detector in [[#Exercise-3_002e74][Exercise 3.74]] proves to be insufficient, because the noisy signal from the sensor leads to spurious zero crossings. Lem E. Tweakit, a hardware specialist, suggests that Alyssa smooth the signal to filter out the noise before extracting the zero crossings. Alyssa takes his advice and decides to extract the zero crossings from the signal constructed by averaging each value of the sense data with the previous value. She explains the problem to her assistant, Louis Reasoner, who attempts to implement the idea, altering Alyssa's program as follows:

  #+BEGIN_SRC lisp
      (define (make-zero-crossings 
               input-stream last-value)
        (let ((avpt 
               (/ (+ (stream-car input-stream) 
                     last-value) 
                  2)))
          (cons-stream 
           (sign-change-detector avpt last-value)
           (make-zero-crossings 
            (stream-cdr input-stream) avpt))))
  #+END_SRC

  This does not correctly implement Alyssa's plan. Find the bug that Louis has installed and fix it without changing the structure of the program. (Hint: You will need to increase the number of arguments to =make-zero-crossings=.)
#+END_QUOTE

#+BEGIN_QUOTE
  *<<Exercise-3_002e76>>Exercise 3.76:* Eva Lu Ator has a criticism of Louis's approach in [[#Exercise-3_002e75][Exercise 3.75]]. The program he wrote is not modular, because it intermixes the operation of smoothing with the zero-crossing extraction. For example, the extractor should not have to be changed if Alyssa finds a better way to condition her input signal. Help Louis by writing a procedure =smooth= that takes a stream as input and produces a stream in which each element is the average of two successive input stream elements. Then use =smooth= as a component to implement the zero-crossing detector in a more modular style.
#+END_QUOTE

<<g_t3_002e5_002e4>> <<Streams-and-Delayed-Evaluation>>
**** 3.5.4Streams and Delayed Evaluation
     :PROPERTIES:
     :CUSTOM_ID: streams-and-delayed-evaluation
     :CLASS: subsection
     :END:

The =integral= procedure at the end of the preceding section shows how we can use streams to model signal-processing systems that contain feedback loops. The feedback loop for the adder shown in [[#Figure-3_002e32][Figure 3.32]] is modeled by the fact that =integral='s internal stream =int= is defined in terms of itself:

#+BEGIN_SRC lisp
    (define int
      (cons-stream 
       initial-value
       (add-streams 
        (scale-stream integrand dt) int)))
#+END_SRC

The interpreter's ability to deal with such an implicit definition depends on the =delay= that is incorporated into =cons-stream=. Without this =delay=, the interpreter could not construct =int= before evaluating both arguments to =cons-stream=, which would require that =int= already be defined. In general, =delay= is crucial for using streams to model signal-processing systems that contain loops. Without =delay=, our models would have to be formulated so that the inputs to any signal-processing component would be fully evaluated before the output could be produced. This would outlaw loops.

Unfortunately, stream models of systems with loops may require uses of =delay= beyond the “hidden” =delay= supplied by =cons-stream=. For instance, [[#Figure-3_002e34][Figure 3.34]] shows a signal-processing system for solving the differential equation [[file:fig/math/369c65256643c82dc3e614bb4c895ea7.svg]] where [[file:fig/math/7a8d165d31a04fc319968e0213091ff8.svg]] is a given function. The figure shows a mapping component, which applies [[file:fig/math/7a8d165d31a04fc319968e0213091ff8.svg]] to its input signal, linked in a feedback loop to an integrator in a manner very similar to that of the analog computer circuits that are actually used to solve such equations.

#+CAPTION: *Figure 3.34:* An “analog computer circuit” that solves the equation [[file:fig/math/369c65256643c82dc3e614bb4c895ea7.svg]].
[[file:fig/chap3/Fig3.34.std.svg]]

Assuming we are given an initial value [[file:fig/math/d78d283068e4a83e3450cbee5438d9f0.svg]] for [[file:fig/math/05e4cdb2f26a4f66b68c167423907fea.svg]], we could try to model this system using the procedure

#+BEGIN_SRC lisp
    (define (solve f y0 dt)
      (define y (integral dy y0 dt))
      (define dy (stream-map f y))
      y)
#+END_SRC

This procedure does not work, because in the first line of =solve= the call to =integral= requires that the input =dy= be defined, which does not happen until the second line of =solve=.

On the other hand, the intent of our definition does make sense, because we can, in principle, begin to generate the =y= stream without knowing =dy=. Indeed, =integral= and many other stream operations have properties similar to those of =cons-stream=, in that we can generate part of the answer given only partial information about the arguments. For =integral=, the first element of the output stream is the specified =initial-value=. Thus, we can generate the first element of the output stream without evaluating the integrand =dy=. Once we know the first element of =y=, the =stream-map= in the second line of =solve= can begin working to generate the first element of =dy=, which will produce the next element of =y=, and so on.

To take advantage of this idea, we will redefine =integral= to expect the integrand stream to be a <<index-delayed-argument>> /delayed argument/. =Integral= will =force= the integrand to be evaluated only when it is required to generate more than the first element of the output stream:

#+BEGIN_SRC lisp
    (define (integral
             delayed-integrand initial-value dt)
      (define int
        (cons-stream 
         initial-value
         (let ((integrand 
                (force delayed-integrand)))
           (add-streams 
            (scale-stream integrand dt)
            int))))
      int)
#+END_SRC

Now we can implement our =solve= procedure by delaying the evaluation of =dy= in the definition of =y=:^{[[#FOOT199][199]]}

#+BEGIN_SRC lisp
    (define (solve f y0 dt)
      (define y (integral (delay dy) y0 dt))
      (define dy (stream-map f y))
      y)
#+END_SRC

In general, every caller of =integral= must now =delay= the integrand argument. We can demonstrate that the =solve= procedure works by approximating [[file:fig/math/58dc533f92bdb70d1c52b693abb23c8a.svg]] by computing the value at [[file:fig/math/5c33785cdb067265921a72a7196bdffb.svg]] of the solution to the differential equation [[file:fig/math/b161bd4a0969f63ba846bcc67cc478f8.svg]] with initial condition [[file:fig/math/0baeaa2084bf6f32ebb3409ba3266885.svg]]:

#+BEGIN_SRC lisp
    (stream-ref 
     (solve (lambda (y) y) 1 0.001) 1000)
    2.716924
#+END_SRC

#+BEGIN_QUOTE
  *<<Exercise-3_002e77>>Exercise 3.77:* The =integral= procedure used above was analogous to the “implicit” definition of the infinite stream of integers in [[#g_t3_002e5_002e2][3.5.2]]. Alternatively, we can give a definition of =integral= that is more like =integers-starting-from= (also in [[#g_t3_002e5_002e2][3.5.2]]):

  #+BEGIN_SRC lisp
      (define (integral
               integrand initial-value dt)
        (cons-stream 
         initial-value
         (if (stream-null? integrand)
             the-empty-stream
             (integral 
              (stream-cdr integrand)
              (+ (* dt (stream-car integrand))
                 initial-value)
              dt))))
  #+END_SRC

  When used in systems with loops, this procedure has the same problem as does our original version of =integral=. Modify the procedure so that it expects the =integrand= as a delayed argument and hence can be used in the =solve= procedure shown above.
#+END_QUOTE

#+BEGIN_QUOTE
  *<<Exercise-3_002e78>>Exercise 3.78:* Consider the problem of designing a signal-processing system to study the homogeneous second-order linear differential equation [[file:fig/math/90859542054384fb46d1f8f369de11cd.svg]] The output stream, modeling [[file:fig/math/05e4cdb2f26a4f66b68c167423907fea.svg]], is generated by a network that contains a loop. This is because the value of [[file:fig/math/afe5f0e8d7ef47b24566ba8394cbe748.svg]] depends upon the values of [[file:fig/math/05e4cdb2f26a4f66b68c167423907fea.svg]] and [[file:fig/math/f3bb169a70611c53f84d2b21eb874a9f.svg]] and both of these are determined by integrating [[file:fig/math/afe5f0e8d7ef47b24566ba8394cbe748.svg]]. The diagram we would like to encode is shown in [[#Figure-3_002e35][Figure 3.35]]. Write a procedure =solve-2nd= that takes as arguments the constants [[file:fig/math/09009cdd5fc245e05305bc574dcdc97d.svg]], [[file:fig/math/3e92f417ccfc1f59b0ee22d034c85747.svg]], and [[file:fig/math/d3c1074774c34911f99ecb81f361716d.svg]] and the initial values [[file:fig/math/d78d283068e4a83e3450cbee5438d9f0.svg]] and [[file:fig/math/6a9ce8b31e43c31b5865a59fbd5cb3f1.svg]] for [[file:fig/math/05e4cdb2f26a4f66b68c167423907fea.svg]] and [[file:fig/math/f3bb169a70611c53f84d2b21eb874a9f.svg]] and generates the stream of successive values of [[file:fig/math/05e4cdb2f26a4f66b68c167423907fea.svg]].
#+END_QUOTE

#+CAPTION: *Figure 3.35:* Signal-flow diagram for the solution to a second-order linear differential equation.
[[file:fig/chap3/Fig3.35b.std.svg]]

#+BEGIN_QUOTE
  *<<Exercise-3_002e79>>Exercise 3.79:* Generalize the =solve-2nd= procedure of [[#Exercise-3_002e78][Exercise 3.78]] so that it can be used to solve general second-order differential equations [[file:fig/math/b654835d20ce39c5092d1afb68353d19.svg]].
#+END_QUOTE

#+CAPTION: *Figure 3.36:* A series RLC circuit.
[[file:fig/chap3/Fig3.36.std.svg]]

#+BEGIN_QUOTE
  *<<Exercise-3_002e80>>Exercise 3.80:* A <<index-series-RLC-circuit>> /series RLC circuit/ consists of a resistor, a capacitor, and an inductor connected in series, as shown in [[#Figure-3_002e36][Figure 3.36]]. If [[file:fig/math/0048ca7d12c133aea4d1304bedc83542.svg]], [[file:fig/math/a23a2cd0225c802f8f86942524b01811.svg]], and [[file:fig/math/684e3ac6700a3aa191a0a61645d5b384.svg]] are the resistance, inductance, and capacitance, then the relations between voltage [[file:fig/math/e60c7c41b53ab571893d9f666b4d9ec5.svg]] and current [[file:fig/math/af841759f02507509a29ae8f8155f403.svg]] for the three components are described by the equations [[file:fig/math/12dfb0f5ee516916136a060538a0aa31.svg]] and the circuit connections dictate the relations [[file:fig/math/467392807bae53e72227a1e247839d35.svg]] Combining these equations shows that the state of the circuit (summarized by [[file:fig/math/437922720b0dba9789c91788e01371b4.svg]], the voltage across the capacitor, and [[file:fig/math/db14b23a580470d18432b8291f88c8be.svg]], the current in the inductor) is described by the pair of differential equations [[file:fig/math/153231e9c665f421ef741075f8ab50b7.svg]] The signal-flow diagram representing this system of differential equations is shown in [[#Figure-3_002e37][Figure 3.37]].
#+END_QUOTE

#+CAPTION: *Figure 3.37:* A signal-flow diagram for the solution to a series RLC circuit.
[[file:fig/chap3/Fig3.37.std.svg]]

#+BEGIN_QUOTE
  Write a procedure =RLC= that takes as arguments the parameters [[file:fig/math/0048ca7d12c133aea4d1304bedc83542.svg]], [[file:fig/math/a23a2cd0225c802f8f86942524b01811.svg]], and [[file:fig/math/684e3ac6700a3aa191a0a61645d5b384.svg]] of the circuit and the time increment [[file:fig/math/d3c1074774c34911f99ecb81f361716d.svg]]. In a manner similar to that of the =RC= procedure of [[#Exercise-3_002e73][Exercise 3.73]], =RLC= should produce a procedure that takes the initial values of the state variables, [[file:fig/math/2eb9d261e24929438737510d0332a8dd.svg]] and [[file:fig/math/a9130d0d371d033d08b57c593f6a2e33.svg]], and produces a pair (using =cons=) of the streams of states [[file:fig/math/437922720b0dba9789c91788e01371b4.svg]] and [[file:fig/math/db14b23a580470d18432b8291f88c8be.svg]]. Using =RLC=, generate the pair of streams that models the behavior of a series RLC circuit with [[file:fig/math/0048ca7d12c133aea4d1304bedc83542.svg]] = 1 ohm, [[file:fig/math/684e3ac6700a3aa191a0a61645d5b384.svg]] = 0.2 farad, [[file:fig/math/a23a2cd0225c802f8f86942524b01811.svg]] = 1 henry, [[file:fig/math/d3c1074774c34911f99ecb81f361716d.svg]] = 0.1 second, and initial values [[file:fig/math/a9130d0d371d033d08b57c593f6a2e33.svg]] = 0 amps and [[file:fig/math/2eb9d261e24929438737510d0332a8dd.svg]] = 10 volts.
#+END_QUOTE

<<Normal_002dorder-evaluation>>
***** Normal-order evaluation
      :PROPERTIES:
      :CUSTOM_ID: normal-order-evaluation
      :CLASS: subsubheading
      :END:

The examples in this section illustrate how the explicit use of =delay= and =force= provides great programming flexibility, but the same examples also show how this can make our programs more complex. Our new =integral= procedure, for instance, gives us the power to model systems with loops, but we must now remember that =integral= should be called with a delayed integrand, and every procedure that uses =integral= must be aware of this. In effect, we have created two classes of procedures: ordinary procedures and procedures that take delayed arguments. In general, creating separate classes of procedures forces us to create separate classes of higher-order procedures as well.^{[[#FOOT200][200]]}

One way to avoid the need for two different classes of procedures is to make all procedures take delayed arguments. We could adopt a model of evaluation in which all arguments to procedures are automatically delayed and arguments are forced only when they are actually needed (for example, when they are required by a primitive operation). This would transform our language to use normal-order evaluation, which we first described when we introduced the substitution model for evaluation in [[file:1_002e1.xhtml#g_t1_002e1_002e5][1.1.5]]. Converting to normal-order evaluation provides a uniform and elegant way to simplify the use of delayed evaluation, and this would be a natural strategy to adopt if we were concerned only with stream processing. In [[file:4_002e2.xhtml#g_t4_002e2][4.2]], after we have studied the evaluator, we will see how to transform our language in just this way. Unfortunately, including delays in procedure calls wreaks havoc with our ability to design programs that depend on the order of events, such as programs that use assignment, mutate data, or perform input or output. Even the single =delay= in =cons-stream= can cause great confusion, as illustrated by [[#Exercise-3_002e51][Exercise 3.51]] and [[#Exercise-3_002e52][Exercise 3.52]]. As far as anyone knows, mutability and delayed evaluation do not mix well in programming languages, and devising ways to deal with both of these at once is an active area of research.

<<g_t3_002e5_002e5>> <<Modularity-of-Functional-Programs-and-Modularity-of-Objects>>
**** 3.5.5Modularity of Functional Programs and Modularity of Objects
     :PROPERTIES:
     :CUSTOM_ID: modularity-of-functional-programs-and-modularity-of-objects
     :CLASS: subsection
     :END:

As we saw in [[file:3_002e1.xhtml#g_t3_002e1_002e2][3.1.2]], one of the major benefits of introducing assignment is that we can increase the modularity of our systems by encapsulating, or “hiding,” parts of the state of a large system within local variables. Stream models can provide an equivalent modularity without the use of assignment. As an illustration, we can reimplement the Monte Carlo estimation of [[file:fig/math/c25469cd205e957b38b51203870f48af.svg]], which we examined in [[file:3_002e1.xhtml#g_t3_002e1_002e2][3.1.2]], from a stream-processing point of view.

The key modularity issue was that we wished to hide the internal state of a random-number generator from programs that used random numbers. We began with a procedure =rand-update=, whose successive values furnished our supply of random numbers, and used this to produce a random-number generator:

#+BEGIN_SRC lisp
    (define rand
      (let ((x random-init))
        (lambda ()
          (set! x (rand-update x))
          x)))
#+END_SRC

In the stream formulation there is no random-number generator /per se/, just a stream of random numbers produced by successive calls to =rand-update=:

#+BEGIN_SRC lisp
    (define random-numbers
      (cons-stream random-init
                   (stream-map rand-update 
                               random-numbers)))
#+END_SRC

We use this to construct the stream of outcomes of the Cesàro experiment performed on consecutive pairs in the =random-numbers= stream:

#+BEGIN_SRC lisp
    (define cesaro-stream
      (map-successive-pairs
       (lambda (r1 r2) (= (gcd r1 r2) 1))
       random-numbers))

    (define (map-successive-pairs f s)
      (cons-stream
       (f (stream-car s) 
          (stream-car (stream-cdr s)))
       (map-successive-pairs 
        f (stream-cdr (stream-cdr s)))))
#+END_SRC

The =cesaro-stream= is now fed to a =monte-carlo= procedure, which produces a stream of estimates of probabilities. The results are then converted into a stream of estimates of [[file:fig/math/c25469cd205e957b38b51203870f48af.svg]]. This version of the program doesn't need a parameter telling how many trials to perform. Better estimates of [[file:fig/math/c25469cd205e957b38b51203870f48af.svg]] (from performing more experiments) are obtained by looking farther into the =pi= stream:

#+BEGIN_SRC lisp
    (define (monte-carlo experiment-stream 
                         passed 
                         failed)
      (define (next passed failed)
        (cons-stream
         (/ passed (+ passed failed))
         (monte-carlo
          (stream-cdr experiment-stream) 
          passed 
          failed)))
      (if (stream-car experiment-stream)
          (next (+ passed 1) failed)
          (next passed (+ failed 1))))

    (define pi
      (stream-map
       (lambda (p) (sqrt (/ 6 p)))
       (monte-carlo cesaro-stream 0 0)))
#+END_SRC

There is considerable modularity in this approach, because we still can formulate a general =monte-carlo= procedure that can deal with arbitrary experiments. Yet there is no assignment or local state.

#+BEGIN_QUOTE
  *<<Exercise-3_002e81>>Exercise 3.81:* [[file:3_002e1.xhtml#Exercise-3_002e6][Exercise 3.6]] discussed generalizing the random-number generator to allow one to reset the random-number sequence so as to produce repeatable sequences of “random” numbers. Produce a stream formulation of this same generator that operates on an input stream of requests to =generate= a new random number or to =reset= the sequence to a specified value and that produces the desired stream of random numbers. Don't use assignment in your solution.
#+END_QUOTE

#+BEGIN_QUOTE
  *<<Exercise-3_002e82>>Exercise 3.82:* Redo [[file:3_002e1.xhtml#Exercise-3_002e5][Exercise 3.5]] on Monte Carlo integration in terms of streams. The stream version of =estimate-integral= will not have an argument telling how many trials to perform. Instead, it will produce a stream of estimates based on successively more trials.
#+END_QUOTE

<<A-functional_002dprogramming-view-of-time>>
***** A functional-programming view of time
      :PROPERTIES:
      :CUSTOM_ID: a-functional-programming-view-of-time
      :CLASS: subsubheading
      :END:

Let us now return to the issues of objects and state that were raised at the beginning of this chapter and examine them in a new light. We introduced assignment and mutable objects to provide a mechanism for modular construction of programs that model systems with state. We constructed computational objects with local state variables and used assignment to modify these variables. We modeled the temporal behavior of the objects in the world by the temporal behavior of the corresponding computational objects.

Now we have seen that streams provide an alternative way to model objects with local state. We can model a changing quantity, such as the local state of some object, using a stream that represents the time history of successive states. In essence, we represent time explicitly, using streams, so that we decouple time in our simulated world from the sequence of events that take place during evaluation. Indeed, because of the presence of =delay= there may be little relation between simulated time in the model and the order of events during the evaluation.

In order to contrast these two approaches to modeling, let us reconsider the implementation of a “withdrawal processor” that monitors the balance in a bank account. In [[file:3_002e1.xhtml#g_t3_002e1_002e3][3.1.3]] we implemented a simplified version of such a processor:

#+BEGIN_SRC lisp
    (define (make-simplified-withdraw balance)
      (lambda (amount)
        (set! balance (- balance amount))
        balance))
#+END_SRC

Calls to =make-simplified-withdraw= produce computational objects, each with a local state variable =balance= that is decremented by successive calls to the object. The object takes an =amount= as an argument and returns the new balance. We can imagine the user of a bank account typing a sequence of inputs to such an object and observing the sequence of returned values shown on a display screen.

Alternatively, we can model a withdrawal processor as a procedure that takes as input a balance and a stream of amounts to withdraw and produces the stream of successive balances in the account:

#+BEGIN_SRC lisp
    (define (stream-withdraw balance amount-stream)
      (cons-stream
       balance
       (stream-withdraw 
        (- balance (stream-car amount-stream))
        (stream-cdr amount-stream))))
#+END_SRC

=Stream-withdraw= implements a well-defined mathematical function whose output is fully determined by its input. Suppose, however, that the input =amount-stream= is the stream of successive values typed by the user and that the resulting stream of balances is displayed. Then, from the perspective of the user who is typing values and watching results, the stream process has the same behavior as the object created by =make-simplified-withdraw=. However, with the stream version, there is no assignment, no local state variable, and consequently none of the theoretical difficulties that we encountered in [[file:3_002e1.xhtml#g_t3_002e1_002e3][3.1.3]]. Yet the system has state!

This is really remarkable. Even though =stream-withdraw= implements a well-defined mathematical function whose behavior does not change, the user's perception here is one of interacting with a system that has a changing state. One way to resolve this paradox is to realize that it is the user's temporal existence that imposes state on the system. If the user could step back from the interaction and think in terms of streams of balances rather than individual transactions, the system would appear stateless.^{[[#FOOT201][201]]}

From the point of view of one part of a complex process, the other parts appear to change with time. They have hidden time-varying local state. If we wish to write programs that model this kind of natural decomposition in our world (as we see it from our viewpoint as a part of that world) with structures in our computer, we make computational objects that are not functional---they must change with time. We model state with local state variables, and we model the changes of state with assignments to those variables. By doing this we make the time of execution of a computation model time in the world that we are part of, and thus we get “objects” in our computer.

Modeling with objects is powerful and intuitive, largely because this matches the perception of interacting with a world of which we are part. However, as we've seen repeatedly throughout this chapter, these models raise thorny problems of constraining the order of events and of synchronizing multiple processes. The possibility of avoiding these problems has stimulated the development of <<index-functional-programming-languages>> /functional programming languages/, which do not include any provision for assignment or mutable data. In such a language, all procedures implement well-defined mathematical functions of their arguments, whose behavior does not change. The functional approach is extremely attractive for dealing with concurrent systems.^{[[#FOOT202][202]]}

On the other hand, if we look closely, we can see time-related problems creeping into functional models as well. One particularly troublesome area arises when we wish to design interactive systems, especially ones that model interactions between independent entities. For instance, consider once more the implementation of a banking system that permits joint bank accounts. In a conventional system using assignment and objects, we would model the fact that Peter and Paul share an account by having both Peter and Paul send their transaction requests to the same bank-account object, as we saw in [[file:3_002e1.xhtml#g_t3_002e1_002e3][3.1.3]]. From the stream point of view, where there are no “objects” /per se/, we have already indicated that a bank account can be modeled as a process that operates on a stream of transaction requests to produce a stream of responses. Accordingly, we could model the fact that Peter and Paul have a joint bank account by merging Peter's stream of transaction requests with Paul's stream of requests and feeding the result to the bank-account stream process, as shown in [[#Figure-3_002e38][Figure 3.38]].

#+CAPTION: *Figure 3.38:* A joint bank account, modeled by merging two streams of transaction requests.
[[file:fig/chap3/Fig3.38a.std.svg]]

The trouble with this formulation is in the notion of <<index-merge>> /merge/. It will not do to merge the two streams by simply taking alternately one request from Peter and one request from Paul. Suppose Paul accesses the account only very rarely. We could hardly force Peter to wait for Paul to access the account before he could issue a second transaction. However such a merge is implemented, it must interleave the two transaction streams in some way that is constrained by “real time” as perceived by Peter and Paul, in the sense that, if Peter and Paul meet, they can agree that certain transactions were processed before the meeting, and other transactions were processed after the meeting.^{[[#FOOT203][203]]} This is precisely the same constraint that we had to deal with in [[file:3_002e4.xhtml#g_t3_002e4_002e1][3.4.1]], where we found the need to introduce explicit synchronization to ensure a “correct” order of events in concurrent processing of objects with state. Thus, in an attempt to support the functional style, the need to merge inputs from different agents reintroduces the same problems that the functional style was meant to eliminate.

We began this chapter with the goal of building computational models whose structure matches our perception of the real world we are trying to model. We can model the world as a collection of separate, time-bound, interacting objects with state, or we can model the world as a single, timeless, stateless unity. Each view has powerful advantages, but neither view alone is completely satisfactory. A grand unification has yet to emerge.^{[[#FOOT204][204]]}

**** Footnotes
     :PROPERTIES:
     :CUSTOM_ID: footnotes
     :CLASS: footnotes-heading
     :END:

[[#DOCF180][^{180}]] Physicists sometimes adopt this view by introducing the “world lines” of particles as a device for reasoning about motion. We've also already mentioned ([[file:2_002e2.xhtml#g_t2_002e2_002e3][2.2.3]]) that this is the natural way to think about signal-processing systems. We will explore applications of streams to signal processing in [[#g_t3_002e5_002e3][3.5.3]].

[[#DOCF181][^{181}]] Assume that we have a predicate =prime?= (e.g., as in [[file:1_002e2.xhtml#g_t1_002e2_002e6][1.2.6]]) that tests for primality.

[[#DOCF182][^{182}]] In the MIT implementation, =the-empty-stream= is the same as the empty list ='()=, and =stream-null?= is the same as =null?=.

[[#DOCF183][^{183}]] This should bother you. The fact that we are defining such similar procedures for streams and lists indicates that we are missing some underlying abstraction. Unfortunately, in order to exploit this abstraction, we will need to exert finer control over the process of evaluation than we can at present. We will discuss this point further at the end of [[#g_t3_002e5_002e4][3.5.4]]. In [[file:4_002e2.xhtml#g_t4_002e2][4.2]], we'll develop a framework that unifies lists and streams.

[[#DOCF184][^{184}]] Although =stream-car= and =stream-cdr= can be defined as procedures, =cons-stream= must be a special form. If =cons-stream= were a procedure, then, according to our model of evaluation, evaluating =(cons-stream ⟨a⟩ ⟨b⟩)= would automatically cause =⟨=b=⟩= to be evaluated, which is precisely what we do not want to happen. For the same reason, =delay= must be a special form, though =force= can be an ordinary procedure.

[[#DOCF185][^{185}]] The numbers shown here do not really appear in the delayed expression. What actually appears is the original expression, in an environment in which the variables are bound to the appropriate numbers. For example, =(+ low 1)= with =low= bound to 10,000 actually appears where =10001= is shown.

[[#DOCF186][^{186}]] There are many possible implementations of streams other than the one described in this section. Delayed evaluation, which is the key to making streams practical, was inherent in Algol 60's <<index-call_002dby_002dname>> /call-by-name/ parameter-passing method. The use of this mechanism to implement streams was first described by [[file:References.xhtml#Landin-_00281965_0029][Landin (1965)]]. Delayed evaluation for streams was introduced into Lisp by [[file:References.xhtml#Friedman-and-Wise-_00281976_0029][Friedman and Wise (1976)]]. In their implementation, =cons= always delays evaluating its arguments, so that lists automatically behave as streams. The memoizing optimization is also known as <<index-call_002dby_002dneed>> /call-by-need/. The Algol community would refer to our original delayed objects as <<index-call_002dby_002dname-thunks>> /call-by-name thunks/ and to the optimized versions as <<index-call_002dby_002dneed-thunks>> /call-by-need thunks/.

[[#DOCF187][^{187}]] Exercises such as [[#Exercise-3_002e51][Exercise 3.51]] and [[#Exercise-3_002e52][Exercise 3.52]] are valuable for testing our understanding of how =delay= works. On the other hand, intermixing delayed evaluation with printing---and, even worse, with assignment---is extremely confusing, and instructors of courses on computer languages have traditionally tormented their students with examination questions such as the ones in this section. Needless to say, writing programs that depend on such subtleties is odious programming style. Part of the power of stream processing is that it lets us ignore the order in which events actually happen in our programs. Unfortunately, this is precisely what we cannot afford to do in the presence of assignment, which forces us to be concerned with time and change.

[[#DOCF188][^{188}]] Eratosthenes, a third-century B.C. Alexandrian Greek philosopher, is famous for giving the first accurate estimate of the circumference of the Earth, which he computed by observing shadows cast at noon on the day of the summer solstice. Eratosthenes's sieve method, although ancient, has formed the basis for special-purpose hardware “sieves” that, until recently, were the most powerful tools in existence for locating large primes. Since the 70s, however, these methods have been superseded by outgrowths of the probabilistic techniques discussed in [[file:1_002e2.xhtml#g_t1_002e2_002e6][1.2.6]].

[[#DOCF189][^{189}]] We have named these figures after Peter Henderson, who was the first person to show us diagrams of this sort as a way of thinking about stream processing. Each solid line represents a stream of values being transmitted. The dashed line from the =car= to the =cons= and the =filter= indicates that this is a single value rather than a stream.

[[#DOCF190][^{190}]] This uses the generalized version of =stream-map= from [[#Exercise-3_002e50][Exercise 3.50]].

[[#DOCF191][^{191}]] This last point is very subtle and relies on the fact that [[file:fig/math/6436915ef69e145d40ea5dfd3337bb17.svg]] (Here, [[file:fig/math/b3d6c43da478408539388ac8f8db402c.svg]] denotes the [[file:fig/math/a99501773dcc30d9f89e17ff1338d1eb.svg]] prime.) Estimates such as these are very difficult to establish. The ancient proof by Euclid that there are an infinite number of primes shows that [[file:fig/math/ae6b05513bef68ea97b8fb8f1a1cf672.svg]], and no substantially better result was proved until 1851, when the Russian mathematician P. L. Chebyshev established that [[file:fig/math/d0a5fba318e74a6585f3ce2f07888251.svg]] for all [[file:fig/math/0932467390da34555ec70c122d7e915e.svg]]. This result, originally conjectured in 1845, is known as <<index-Bertrand_0027s-hypothesis>> /Bertrand's hypothesis/. A proof can be found in section 22.3 of [[file:References.xhtml#Hardy-and-Wright-1960][Hardy and Wright 1960]].

[[#DOCF192][^{192}]] This exercise shows how call-by-need is closely related to ordinary memoization as described in [[file:3_002e3.xhtml#Exercise-3_002e27][Exercise 3.27]]. In that exercise, we used assignment to explicitly construct a local table. Our call-by-need stream optimization effectively constructs such a table automatically, storing values in the previously forced parts of the stream.

[[#DOCF193][^{193}]] We can't use =let= to bind the local variable =guesses=, because the value of =guesses= depends on =guesses= itself. [[#Exercise-3_002e63][Exercise 3.63]] addresses why we want a local variable here.

[[#DOCF194][^{194}]] As in [[file:2_002e2.xhtml#g_t2_002e2_002e3][2.2.3]], we represent a pair of integers as a list rather than a Lisp pair.

[[#DOCF195][^{195}]] See [[#Exercise-3_002e68][Exercise 3.68]] for some insight into why we chose this decomposition.

[[#DOCF196][^{196}]] The precise statement of the required property on the order of combination is as follows: There should be a function [[file:fig/math/7a8d165d31a04fc319968e0213091ff8.svg]] of two arguments such that the pair corresponding to element [[file:fig/math/aa5dfb3bb62785181553d83502ccb9ec.svg]] of the first stream and element [[file:fig/math/c43c70f4cf72247cdeb1dd81fb4f1ee0.svg]] of the second stream will appear as element number [[file:fig/math/f26c98926523adf21133e1fdee541646.svg]] of the output stream. The trick of using =interleave= to accomplish this was shown to us by David Turner, who employed it in the language KRC ([[file:References.xhtml#Turner-1981][Turner 1981]]).

[[#DOCF197][^{197}]] We will require that the weighting function be such that the weight of a pair increases as we move out along a row or down along a column of the array of pairs.

[[#DOCF198][^{198}]] To quote from G. H. Hardy's obituary of Ramanujan ([[file:References.xhtml#Hardy-1921][Hardy 1921]]): “It was Mr. Littlewood (I believe) who remarked that ‘every positive integer was one of his friends.' I remember once going to see him when he was lying ill at Putney. I had ridden in taxi-cab No. 1729, and remarked that the number seemed to me a rather dull one, and that I hoped it was not an unfavorable omen. ‘No,' he replied, ‘it is a very interesting number; it is the smallest number expressible as the sum of two cubes in two different ways.' ” The trick of using weighted pairs to generate the Ramanujan numbers was shown to us by Charles Leiserson.

[[#DOCF199][^{199}]] This procedure is not guaranteed to work in all Scheme implementations, although for any implementation there is a simple variation that will work. The problem has to do with subtle differences in the ways that Scheme implementations handle internal definitions. (See [[file:4_002e1.xhtml#g_t4_002e1_002e6][4.1.6]].)

[[#DOCF200][^{200}]] This is a small reflection, in Lisp, of the difficulties that conventional strongly typed languages such as Pascal have in coping with higher-order procedures. In such languages, the programmer must specify the data types of the arguments and the result of each procedure: number, logical value, sequence, and so on. Consequently, we could not express an abstraction such as “map a given procedure =proc= over all the elements in a sequence” by a single higher-order procedure such as =stream-map=. Rather, we would need a different mapping procedure for each different combination of argument and result data types that might be specified for a =proc=. Maintaining a practical notion of “data type” in the presence of higher-order procedures raises many difficult issues. One way of dealing with this problem is illustrated by the language ML ([[file:References.xhtml#Gordon-et-al_002e-1979][Gordon et al. 1979]]), whose “polymorphic data types” include templates for higher-order transformations between data types. Moreover, data types for most procedures in ML are never explicitly declared by the programmer. Instead, ML includes a <<index-type_002dinferencing>> /type-inferencing/ mechanism that uses information in the environment to deduce the data types for newly defined procedures.

[[#DOCF201][^{201}]] Similarly in physics, when we observe a moving particle, we say that the position (state) of the particle is changing. However, from the perspective of the particle's world line in space-time there is no change involved.

[[#DOCF202][^{202}]] John Backus, the inventor of Fortran, gave high visibility to functional programming when he was awarded the ACM Turing award in 1978. His acceptance speech ([[file:References.xhtml#Backus-1978][Backus 1978]]) strongly advocated the functional approach. A good overview of functional programming is given in [[file:References.xhtml#Henderson-1980][Henderson 1980]] and in [[file:References.xhtml#Darlington-et-al_002e-1982][Darlington et al. 1982]].

[[#DOCF203][^{203}]] Observe that, for any two streams, there is in general more than one acceptable order of interleaving. Thus, technically, “merge” is a relation rather than a function---the answer is not a deterministic function of the inputs. We already mentioned ([[file:3_002e4.xhtml#Footnote-167][Footnote 167]]) that nondeterminism is essential when dealing with concurrency. The merge relation illustrates the same essential nondeterminism, from the functional perspective. In [[file:4_002e3.xhtml#g_t4_002e3][4.3]], we will look at nondeterminism from yet another point of view.

[[#DOCF204][^{204}]] The object model approximates the world by dividing it into separate pieces. The functional model does not modularize along object boundaries. The object model is useful when the unshared state of the “objects” is much larger than the state that they share. An example of a place where the object viewpoint fails is quantum mechanics, where thinking of things as individual particles leads to paradoxes and confusions. Unifying the object view with the functional view may have little to do with programming, but rather with fundamental epistemological issues.

Next: [[file:Chapter-4.xhtml#Chapter-4][Chapter 4]], Prev: [[file:3_002e4.xhtml#g_t3_002e4][3.4]], Up: [[#g_t3_002e5][3.5]]   [[[file:index.xhtml#SEC_Contents][Contents]]]

[[#pagebottom][⇣]]<<pagebottom>>
